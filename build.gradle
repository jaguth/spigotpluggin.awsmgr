buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    id "de.undercouch.download" version "3.4.3"
}

apply plugin: 'java'

// for fat jar
apply plugin: 'com.github.johnrengelman.shadow'

group 'com.jaguth'
version '1.1'

sourceCompatibility = 1.8

repositories {
    mavenCentral()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://hub.spigotmc.org/nexus/content/groups/public/' // dependencies required by org.spigotmc:spigot-api:1.14-R0.1-SNAPSHOT
    }
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.14.2-R0.1-SNAPSHOT' // from https://hub.spigotmc.org/nexus/content/repositories/snapshots/

    compile 'com.amazonaws:aws-java-sdk-ec2:1.11.505'
    compile 'com.amazonaws:aws-java-sdk-sts:1.11.505'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.powermock:powermock-module-junit4:2.0.0'
    testCompile 'org.powermock:powermock-api-mockito2:2.0.0'
}

def BUILDTOOLS_VERSION = "1.14.2"
def BUILDTOOLS_DIR = "$projectDir/buildtools"
def PLUGINS_DIR = "$BUILDTOOLS_DIR/plugins"
def PLUGIN_NAME = "spigotpluggin.awsmgr-$version-all.jar"

task deleteFromSpigotServer(type: Delete) {
    delete "$PLUGINS_DIR/$PLUGIN_NAME"
    followSymlinks = true
}
deleteFromSpigotServer.dependsOn shadowJar

task copyToSpigotServer(type: Copy) {
    from file("$buildDir/libs/$PLUGIN_NAME")
    into file("$PLUGINS_DIR")
}
copyToSpigotServer.dependsOn deleteFromSpigotServer

task downloadBuildTools(group: 'spigot', type: Download) {
    src 'https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar'
    dest "buildtools/BuildTools.jar"
}

task createStartSpigotScript (group: 'spigot') {
    File startSpigotBatchFile = new File("$BUILDTOOLS_DIR/startSpigot.cmd")

    if (!startSpigotBatchFile.exists()) {
        def command = "java -Xms512M -Xmx1G -XX:+UseConcMarkSweepGC -jar spigot-1.14.2.jar"
        startSpigotBatchFile.write command
    }
}

task buildThePlugin () {}
buildThePlugin.dependsOn shadowJar, clean

task installThePlugin (group: 'spigot') {}
installThePlugin.dependsOn deleteFromSpigotServer, copyToSpigotServer, buildThePlugin

