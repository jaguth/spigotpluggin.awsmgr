
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.1'
    }
}

plugins {
    id "de.undercouch.download" version "3.4.3"
}

apply plugin: 'java'

// for fat jar
apply plugin: 'com.github.johnrengelman.shadow'

group 'com.jaguth'
version '1.1.2'

sourceCompatibility = 1.8

def BUILDTOOLS_VERSION = "1.14.3"
def BUILDTOOLS_DIR = "$projectDir/buildtools"
def PLUGINS_DIR = "$BUILDTOOLS_DIR/plugins"
def PLUGIN_NAME = "spigotpluggin.awsmgr-$version-all.jar"

repositories {
    mavenCentral()

    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://hub.spigotmc.org/nexus/content/groups/public/' // dependencies required by org.spigotmc:spigot-api:1.14-R0.1-SNAPSHOT
    }
}

dependencies {
    compile "org.spigotmc:spigot-api:$BUILDTOOLS_VERSION-R0.1-SNAPSHOT" // from https://hub.spigotmc.org/nexus/content/repositories/snapshots/

    compile 'com.amazonaws:aws-java-sdk-ec2:1.11.505'
    compile 'com.amazonaws:aws-java-sdk-sts:1.11.505'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.+'
    testCompile 'org.powermock:powermock-module-junit4:2.0.0'
    testCompile 'org.powermock:powermock-api-mockito2:2.0.0'
}



task deleteFromSpigotServer(type: Delete) {
    delete "$PLUGINS_DIR/$PLUGIN_NAME"
    followSymlinks = true
}
deleteFromSpigotServer.dependsOn shadowJar

task copyToSpigotServer(type: Copy) {
    from file("$buildDir/libs/$PLUGIN_NAME")
    into file("$PLUGINS_DIR")
}
copyToSpigotServer.dependsOn deleteFromSpigotServer

task downloadBuildTools(group: 'spigot', type: Download) {
    src 'https://hub.spigotmc.org/jenkins/job/BuildTools/lastSuccessfulBuild/artifact/target/BuildTools.jar'
    dest "buildtools/BuildTools.jar"
}

task buildTheBuildTools(group: 'spigot', dependsOn: jar) << {
    println "It will take a few minutes to build the build tools"

    javaexec {
        workingDir = BUILDTOOLS_DIR
        main = "-jar"
        args '-Xmx1024M', '-jar', "$BUILDTOOLS_DIR/BuildTools.jar", '--rev', BUILDTOOLS_VERSION
    }
}

task buildThePlugin () {}
buildThePlugin.dependsOn shadowJar, clean

task installThePlugin (group: 'spigot') {}
installThePlugin.dependsOn copyToSpigotServer, buildThePlugin, deleteFromSpigotServer

